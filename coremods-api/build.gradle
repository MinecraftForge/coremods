plugins {
    id 'idea'
    id 'eclipse'
    id 'java-library'
    id 'maven-publish'
    alias libs.plugins.license
    //alias libs.plugins.versions
    alias libs.plugins.gradleutils
}

group = 'net.minecraftforge'
version = rootProject.version
println "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
    mavenLocal()
}

license {
    header = rootProject.file('LICENSE-header.txt')
    newLine = false
}

dependencies {
    compileOnly(libs.modlauncher)
    compileOnly(libs.securemodules) // Needed by Modlauncher

    compileOnly(libs.nulls)
    compileOnly(rootProject)

    api(libs.bundles.asm)
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Specification-Title': 'coremods-api',
            'Specification-Vendor': 'Forge Development LLC',
            'Specification-Version': '1', // TODO: Use the tag
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor' :'Forge Development LLC'
        ], 'net/minecraftforge/coremod/api/')
    }
}

publishing {
    publications.register('mavenJava', MavenPublication) {
    	gradleutils.promote(it)
        pom {
            from components.java
            artifactId = 'coremods-api'
            name = 'Core Mods API'
            description = 'API that is useful for Minecraft Transformers'
            url = 'https://github.com/MinecraftForge/coremods'

            gradleutils.pom.addRemoteDetails(pom)
            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.developers.LexManos
                developer gradleutils.pom.developers.cpw
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea.module {
    downloadJavadoc = downloadSources = true
}
